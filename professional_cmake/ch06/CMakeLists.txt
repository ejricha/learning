cmake_minimum_required(VERSION 3.15)

# Set the project name
project(ch06)
message("\n\n[Project ${PROJECT_NAME}]\n")

# Good examples of common if statements
message("Check for MSVC (${MSVC}) instead of WIN32 (${WIN32})")
if (MSVC)
	message("  Compiling for MSVC")
else()
	message("  Not compiling for MSVC")
endif()
message("Check if CMAKE_GENERATOR (${CMAKE_GENERATOR}) is Xcode instead of APPLE (${APPLE})")
if (CMAKE_GENERATOR STREQUAL "Xcode")
	message("  Compiling for Xcode")
else()
	message("  Not compiling for Xcode")
endif()

# For loop with nested if statements
foreach(i RANGE 0 4)
	if(${i} LESS 1)
		message("i (${i}) is less than one")
	elseif(${i} EQUAL 1)
		message("i (${i}) is one")
	elseif(${i} LESS_EQUAL 2)
		message("i (${i}) is between one and two")
	else()
		message("i (${i}) is greater than two")
	endif()
endforeach()

# Set version lists
set(versionA 1.2    1.2    1.2.3  2.0.1  1.8.2  1.7.3)
set(versionB 1.2.0  1.2.3  1.2    1.9.7  2      1.11.1)
list(LENGTH versionA listLength)
math(EXPR lastIndex "${listLength} - 1")
foreach(i RANGE 0 ${lastIndex})
	list(GET versionA ${i} a)
	list(GET versionB ${i} b)
	if(${a} VERSION_LESS ${b})
		message(NOTICE "  ${a} < ${b}")
	elseif(${a} VERSION_EQUAL ${b})
		message(NOTICE "  ${a} == ${b}")
	elseif(${a} VERSION_GREATER ${b})
		message(NOTICE "  ${a} > ${b}")
	else()
		message(FATAL_ERROR "  LOGIC ERROR")
	endif()
endforeach()
